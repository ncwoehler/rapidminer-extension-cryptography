apply plugin: 'java'
apply plugin: 'eclipse'

defaultTasks = ['Install']
	
ext {
	updateServerId = "rmx_" + namespace
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = major + "." + minor + "." + patch 
group = vendor

// create Maven like provided configuration
configurations {
    provided
    compile.extendsFrom provided
}


// minimize changes, at least for now (gradle uses 'build' by default)
buildDir = "target"

repositories {
    mavenCentral()
}

dependencies {
    provided fileTree(dir: rapidminerHome + '/lib', include: '*.jar')
}

jar {
	dependsOn configurations.runtime
	from {
		(configurations.runtime - configurations.provided).collect {
      		it.isDirectory() ? it : zipTree(it)
    	}	
  	}
	manifest {
		attributes("Manifest-Version": 		"1.0",
		"Implementation-Vendor": 	vendor,
		"Implementation-Title":		name,
		"Implementation-URL":		homepage,
		"Implementation-Version": 	version,
		"Specification-Title": 		name,
		"Specification-Version":	version,
		"RapidMiner-Version":		minRMVersion,
		"RapidMiner-Type":			"RapidMiner_Extension",
		"Plugin-Dependencies":		extensionDependencies,
		
		// Definition of important files
		"Extension-ID":				updateServerId,
		"Namespace":				namespace,
		"Initialization-Class":		initClass,
		"IOObject-Descriptor":		objectDefinition,
		"Operator-Descriptor":		operatorDefinition,
		"ParseRule-Descriptor":		parseRuleDefinition,
		"Group-Descriptor":			groupProperties,
		"Error-Descriptor":			errorDescription,
		"UserError-Descriptor":		userErrors,
		"GUI-Descriptor":			guiDescription
		)
	}
}

gradle.taskGraph.whenReady {taskGraph ->
	if (!taskGraph.hasTask('release')) {
		version = major + "." + minor + "." + patch + "-SNAPSHOT"
	}
}

// Copies outputs of jar to the plugins directory of RapidMiner
task install(type: Copy) {
	from jar
	into rapidminerHome + "/lib/plugins"
}

